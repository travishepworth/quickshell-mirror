pragma Singleton
import QtQuick
import Quickshell.Io
import qs.config


QtObject {
  id: stateManager

  readonly property string stateDir: Config.statePath

  /**
     * @brief Creates a state handler for a specific service
     * @param serviceName The name of the service (e.g., "launcher")
     * @returns An object with save/load methods, shared by every caller
     *   asking for the same name (so re-created consumers don't each leak
     *   a FileView)
     */
  function createStateHandler(serviceName) {
    if (_handlers[serviceName])
      return _handlers[serviceName];
    const stateFile = serviceName + ".json";
    const fileView = fileViewComponent.createObject(stateManager, {
      path: Qt.resolvedUrl(stateDir + stateFile)
    });

    const handler = {
      save: function (data) {
        try {
          const stateString = JSON.stringify(data, null, 2);
          fileView.setText(stateString);
          console.log(`[StateManager] ${serviceName} state saved.`);
          return true;
        } catch (e) {
          console.error(`[StateManager] Failed to save ${serviceName}:`, e);
          return false;
        }
      },
      load: function (defaultValue) {
        const filepath = stateDir + stateFile;
        console.log(`[StateManager] Loading ${serviceName} from`, filepath);
        const content = FileManager.read(Qt.resolvedUrl(filepath));

        if (content) {
          try {
            const state = JSON.parse(content);
            console.log(`[StateManager] ${serviceName} loaded successfully.`);
            return state;
          } catch (e) {
            console.error(`[StateManager] Failed to parse ${serviceName}:`, e);
            return defaultValue;
          }
        } else {
          console.log(`[StateManager] No ${serviceName} state found.`);
          return defaultValue;
        }
      }
    };
    _handlers[serviceName] = handler;
    return handler;
  }

  property var _handlers: ({})

  property Component _fileViewComponent: Component {
    id: fileViewComponent
    FileView {
      // A missing state file is normal (nothing saved yet): load() falls
      // back to its default
      printErrors: false
      blockWrites: true
      atomicWrites: true
      onSaveFailed: error => {
        console.error("[StateManager] Save failed:", FileViewError.toString(error));
      }
    }
  }
}
