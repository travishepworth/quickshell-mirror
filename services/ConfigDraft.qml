import QtQuick

// A working copy of one config section for an editor (settings menu, bar
// editor, overlay editor). Edits mutate `local` in place, then call
// changed(); nothing reaches the running config or disk until save().
// Not a singleton: each editor service owns one.
QtObject {
  id: root

  // Key path of the section in the config, e.g. ["Bars"]; [] is all of it
  property var path: []

  // Plain values, filled by load(): a binding here would reset the draft
  // on every config change
  property var local: null
  property var saved: null
  property bool isDirty: false

  Component.onCompleted: load()

  function _clone(value) {
    return JSON.parse(JSON.stringify(value ?? null));
  }

  function _read() {
    let value = ConfigManager.config;
    for (const key of root.path)
      value = value?.[key];
    return value;
  }

  // Start over from the current config
  function load() {
    root.local = _clone(_read());
    root.saved = _clone(root.local);
    root.isDirty = false;
  }

  // After mutating `local` in place: update isDirty, and re-assign it (next
  // tick) so bindings on it see the change, since in-place mutation doesn't
  // notify. Deferred so it doesn't re-enter the binding of the control
  // that made the edit.
  function changed() {
    root.isDirty = JSON.stringify(root.local) !== JSON.stringify(root.saved);
    Qt.callLater(() => root.local = _clone(root.local));
  }

  // Merges `local` onto the LATEST config (so settings edited elsewhere
  // meanwhile survive) and commits it. Returns false, staying dirty, if
  // ConfigManager rejects it.
  function save() {
    let merged = _clone(root.local);
    if (root.path.length > 0) {
      merged = _clone(ConfigManager.config);
      let cur = merged;
      for (let i = 0; i < root.path.length - 1; i++)
        cur = cur[root.path[i]];
      cur[root.path[root.path.length - 1]] = _clone(root.local);
    }
    if (!ConfigManager.commit(merged))
      return false;
    root.saved = _clone(root.local);
    root.isDirty = false;
    return true;
  }
}
